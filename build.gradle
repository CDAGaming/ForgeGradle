plugins {
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

group = 'net.minecraftforge.gradle'
version = '1.0-SNAPSHOT'
archivesBaseName = 'ForgeGradle'
targetCompatibility = '1.6'
sourceCompatibility = '1.6'

repositories {
    maven {
        name = "forge"
        url = "http://files.minecraftforge.net/maven"
    }
    mavenCentral()
}

jar {
    manifest {
        attributes 'version':project.version, 'javaCompliance': project.targetCompatibility
        attributes 'group':project.group
    }
}

configurations {
    deployerJars
}

dependencies {
    compile gradleApi()
    
    // moved to the beginning to be the overrider
    compile 'org.ow2.asm:asm-debug-all:4.1' // Debug version, so we can have generics!

    compile 'net.sourceforge.argo:argo:3.4' // json stuff? perhaps replace with json slurper and stuff?
    compile 'net.sf.opencsv:opencsv:2.3' // reading CSVs.. also used by SpecialSource
    compile 'com.cloudbees:diff4j:1.1' // for difing and patching
    compile 'com.github.abrarsyed.jastyle:jAstyle:1.2' // formatting

    compile 'com.github.jponge:lzma-java:1.3' // replaces the LZMA binary
    compile 'com.nothome:javaxdelta:2.0.1' // GDIFF implementation for BinPatches
    compile 'com.google.code.gson:gson:2.2.4' // Used instead of Argo for buuilding changelog.
    compile 'com.github.tony19:named-regexp:0.2.3' // 1.7 Named regexp features
    
    compile 'net.md-5:SpecialSource:1.6.1' // deobf and reobs
    
    // mcp stuff
    compile 'de.oceanlabs.mcp:RetroGuard:3.6.6'
    compile 'de.oceanlabs.mcp:mcinjector:3.1'

    // stupid maven
    deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
}

artifacts { archives jar }

publishing {
    publications {
        bintray(MavenPublication) {
            from components.java
            artifact jar

            pom {
                name = project.archivesBaseName
                description = 'Gradle plugin for Forge'
                url = 'https://github.com/MinecraftForge/ForgeGradle'

                scm {
                    url = 'https://github.com/anatawa12/ForgeGradle'
                    connection = 'scm:git:git://github.com/anatawa12/ForgeGradle.git'
                    developerConnection = 'scm:git:git@github.com:anatawa12/ForgeGradle.git'
                }

                issueManagement {
                    system = 'github'
                    url = 'https://github.com/anatawa12/ForgeGradle/issues'
                }

                licenses {
                    license {
                        name = 'Lesser GNU Public License, Version 2.1'
                        url = 'https://www.gnu.org/licenses/lgpl-2.1.html'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'AbrarSyed'
                        name = 'Abrar Syed'
                        roles = ['developer']
                    }

                    developer {
                        id = 'LexManos'
                        name = 'Lex Manos'
                        roles = ['developer']
                    }

                    developer {
                        id = 'anatawa12'
                        name = 'anatawa12'
                        roles = ['developer']
                    }
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

if (project.hasProperty("push_release")) {
    bintray {
        user = project.hasProperty("BINTRAY_USER") ? BINTRAY_USER : ""
        key = project.hasProperty("BINTRAY_KEY") ? BINTRAY_KEY : ""
        publications = ["bintray"]

        pkg {
            repo = "maven"
            name = group + "." + project.name
            licenses = ["LGPL-2.1"]
            websiteUrl = "https://github.com/anatawa12/ForgeGradle/"
            issueTrackerUrl = "https://github.com/anatawa12/ForgeGradle/issues"
            vcsUrl = "https://github.com/anatawa12/ForgeGradle.git"
            publicDownloadNumbers = true
            version {
                name = project.version
            }
        }
    }
}

bintrayUpload.dependsOn assemble
